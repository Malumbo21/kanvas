plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

group 'com.athaydes.kanvas'
version project.property('kanvas.core.version')
description 'A Kotlin DSL for JavaFX Canvas'

repositories {
    mavenCentral()
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

sourceCompatibility = '21'
targetCompatibility = '21'

kotlin {
    jvmToolchain(21)
}

jar {
    manifest {
        attributes 'Implementation-Version': version,
                'Implementation-Name': 'Kanvas Core',
                'Implementation-Vendor': 'Renato Athaydes',
                'Automatic-Module-Name': 'kanvas.core'
    }
}

/* Publishing config */

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'kanvas-core'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'kanvas-core'
                description = project.description
                url = 'https://github.com/renatoathaydes/kanvas'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'renatoathaydes'
                        name = 'Renato Athaydes'
                        email = 'renato@athaydes.com'
                    }
                }
                scm {
                    connection = 'git@github.com:renatoathaydes/kanvas.git'
                    developerConnection = 'git@github.com:renatoathaydes/kanvas.git'
                    url = 'https://github.com/renatoathaydes/kanvas'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

javadoc {
    options.addBooleanOption('html5', true)
}
